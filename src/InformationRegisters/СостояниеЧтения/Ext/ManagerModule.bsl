Функция ПолучитьСостояние(ЗНАЧ ФайлЗамера) Экспорт
	Результат = Новый Структура("ПрочитаноСтрок,ЧтениеЗавершено,Размер", 0, Ложь, 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СостояниеЧтения.Размер,
	|	СостояниеЧтения.ЧтениеЗавершено,
	|	СостояниеЧтения.ПрочитаноСтрок
	|ИЗ
	|	РегистрСведений.СостояниеЧтения КАК СостояниеЧтения
	|ГДЕ
	|	СостояниеЧтения.ФайлЗамера = &ФайлЗамера";
	Запрос.УстановитьПараметр("ФайлЗамера", ФайлЗамера);
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса.Выгрузить()[0]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьСостояние(
		ФайлЗамера, 
		ПериодФайла,
		ПрочитаноСтрок, 
		ДатаНачалаЧтения,
		РазмерФайла) Экспорт
		
	//если дата начала чтения больше чем конец часа (запас в 5 минут) 
	//считаем что файл больше дополняться не будет
	ЧтениеЗавершено = ДатаНачалаЧтения > КонецЧаса(ПериодФайла) + 300;
	
	ТекущееСостояние = ПолучитьСостояние(ФайлЗамера);
	Если ТекущееСостояние.ПрочитаноСтрок = ПрочитаноСтрок
		И ТекущееСостояние.ЧтениеЗавершено = ЧтениеЗавершено Тогда
		Возврат;
	КонецЕсли;	
	
	МенеджерЗаписи = РегистрыСведений.СостояниеЧтения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФайлЗамера = ФайлЗамера;
	МенеджерЗаписи.ПрочитаноСтрок = ПрочитаноСтрок;
	МенеджерЗаписи.ЧтениеЗавершено = ЧтениеЗавершено;
	МенеджерЗаписи.Размер = РазмерФайла;
	МенеджерЗаписи.Записать();		
		
	
КонецПроцедуры