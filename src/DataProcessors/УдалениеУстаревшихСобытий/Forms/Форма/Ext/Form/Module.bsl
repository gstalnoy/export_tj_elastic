

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	Список.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Замеры.Ссылка,
	|	НачалоПериода(ДОБАВИТЬКДАТЕ(&ТекущаяДата, День, -Замеры.ГлубинаХранения), ДЕНЬ) КАК ГраничнаяДата
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	Справочник.Замеры КАК Замеры
	|ГДЕ
	|	Замеры.ГлубинаХранения > 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт.Ссылка КАК Замер,
	|	вт.ГраничнаяДата,
	|	КОЛИЧЕСТВО(СобытияЗамера.Ссылка) КАК КоличествоКУдалению
	|ИЗ
	|	вт КАК вт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СобытияЗамера КАК СобытияЗамера
	|		ПО вт.Ссылка = СобытияЗамера.Владелец
	|		И СобытияЗамера.ДатаСобытия < вт.ГраничнаяДата
	|СГРУППИРОВАТЬ ПО
	|	вт.Ссылка,
	|	вт.ГраничнаяДата";
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Список.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПоказатьЗначение(Новый ОписаниеОповещения("ОбработкаВыбора", ЭтотОбъект),Список.НайтиПоИдентификатору(ВыбраннаяСтрока).Замер);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(Результат, Доп) Экспорт
	ОбновитьСписок(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРегламентногоУдаления(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Идентификатор", ПолучитьИдентификаторРЗ());
	ПараметрыФормы.Вставить("Действие", "Изменить");
	ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьУдаление(Команда)
	ЗапуститьУдалениеНаСервере();
	ОбновитьСписок(Неопределено);
	Состояние("Завершено");	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьСписокНаСервере();
КонецПроцедуры


&НаСервере
Процедура ЗапуститьУдалениеНаСервере()
	ОбновлениеДанныхРегламентное.УдалениеУстаревшихСобытий();
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьИдентификаторРЗ()
	Возврат РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.УдалениеУстаревшихСобытий).УникальныйИдентификатор;
КонецФункции
